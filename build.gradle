import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
    id 'java'
    id 'net.ltgt.errorprone' version '4.4.0' apply false
}

group = 'ch.schmucki'
version = '0.0.1-SNAPSHOT'
description = 'kanban'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'net.ltgt.errorprone'

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
        errorprone 'com.google.errorprone:error_prone_core:2.46.0'
        errorprone 'com.uber.nullaway:nullaway:0.12.15'
        implementation 'org.jspecify:jspecify:1.0.0'

    }

    tasks.test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile).configureEach  {
        options.errorprone {
            check("NullAway", CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "com.uber")
        }

        if (name.toLowerCase().contains("test")) {
            options.errorprone {
                disable("NullAway")
            }
        }
    }
}
